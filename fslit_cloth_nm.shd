/*

fslit_aniso_nm.shd

*/
#define PI 3.14159265359
// Set to one to use a tonemapping operator and fake scene exposure.
#define PSEUDO_HDR 1
#define USE_GAMMA_CORRECTION 1
#define saturate(val) clamp((val), 0.0, 1.0)

const int   MAX_LIGHTS     = 8;
const float NO_ATTENUATION = 0.0;
const float fShininessMax  = 80.0;
const float fShininessMin  = 8.0;
const float fHdrExposure   = 1.7;

uniform sampler2D texUnit0;	// decal
uniform sampler2D texUnit1;	// normal map
uniform sampler2D texUnit2;	// specular map

uniform lowp    vec4 fogColor;
uniform         int fogEnabled;
uniform         int texture0Bound;

uniform lowp	vec4  lightAmbient[MAX_LIGHTS];
uniform lowp	vec4  lightDiffuse[MAX_LIGHTS];
uniform mediump	vec4  lightPosition[MAX_LIGHTS];
uniform mediump float lightQuadraticAtten[MAX_LIGHTS];
uniform         int   numLights;

uniform mediump	vec4  materialFrontAmbient;
uniform mediump	vec4  materialFrontDiffuse;
uniform mediump	vec4  materialFrontSpecular;
uniform mediump	vec4  materialFrontEmissive;
uniform lowp    vec4  frontLightModelProductSceneColor;

varying      vec4  vPosOut; // This will be in camera space.
varying      vec2  vTc;
varying lowp vec4  vColorOut;
varying      vec3  vStaticLightDirOut;

varying      vec3  vTangentOut; // Our tangent space basis in camera space.  It has been interpolated, so normalization will be required. 
varying      vec3  vNormalOut;
varying      float fTextureHandedness; // Just the handedness attribute passed from the vertex shader. It should always be the same for all vertices of a face so interpolation will not be an issue.

varying      float fFogFragCoord;

vec3 Ambient;
vec3 Diffuse;
vec3 Specular;

struct Surface {
	vec3  normal;
	vec3  toEye;
	float roughness;
};

struct Light {
	vec3  direction;
	vec3  ambient;
	vec3  color;
	float attenuationCoefficient;
};

vec3 asLinear(in vec3 color) {
#if USE_GAMMA_CORRECTION
	return pow(color, vec3(2.2));
#else
	return color;
#endif
}

vec4 asLinear(in vec4 color) {
#if USE_GAMMA_CORRECTION
	return vec4(asLinear(color.rgb), color.a);
#else
	return color;
#endif
}

vec3 asGamma(in vec3 color) {
#if USE_GAMMA_CORRECTION
	return pow(color, vec3(1.0/2.2));
#else
	return color;
#endif
}

vec4 asGamma(in vec4 color) {
#if USE_GAMMA_CORRECTION
	return vec4(asGamma(color.rgb), color.a);
#else
	return color;
#endif
}

float asRoughness(in float specAlpha) {
	/* HACK: We're converting to a Blinn-Phong exponent and then immediately transitioning to a Beckmann roughness value.
	 * It makes much more sense to move directly from the alpha value directly to roughness coefficient, possibly
	 * using a polynomial/rational function approximation for this conversion. */
	float s = mix(fShininessMin, fShininessMax, specAlpha);
    return clamp(pow(2.0 / (s + 2.0), 0.25), 1e-5, 1.0);
}

// [Ashikhmin, Premoze 2007, Distribution-based BRDFs]
float getAshikhminVisibility(in float nDotE, in float nDotL) {
	return 1.0 / max(4.0 * (nDotL + nDotE - nDotL * nDotE), 1e-4);
}

// [Estevez, Kulla 2017, Production Friendly Microfacet Sheen BRDF]
float getCharlieD(in float nDotH, in float roughness) {
	float rcpR  = 1.0 / roughness;
	float cos2h = nDotH * nDotH;
	float sin2h = 1.0 - cos2h;
	return (2. + rcpR) * pow(sin2h, rcpR * 0.5) / (2.0 * PI);
}

void getContribution(in Surface surface, in Light light) {
	vec3  toLight     = normalize(light.direction);
	vec3  halfVector  = normalize(surface.toEye + toLight);
	float attenuation = 1.0 / (light.attenuationCoefficient * dot(light.direction, light.direction) + 1.0);
	float nDotH       = saturate(dot(surface.normal, halfVector));
	float nDotE       = dot(surface.normal, surface.toEye);
	float nDotL       = dot(surface.normal, toLight);

	float d    = getCharlieD(nDotH, surface.roughness);
	float g    = getAshikhminVisibility(nDotE, nDotL);
	vec3 color = attenuation * light.color;

	Ambient  += attenuation * light.ambient;
	Diffuse  += saturate(nDotL) * color;
	Specular += saturate(nDotL * 3.0) * (d * g) * color;
}

//=============================================================================
//
// computeLighting()
//
// Calculates lighting contribution for all lights.
//
//=============================================================================
void computeLighting(in vec3 normal, in vec3 position, in float roughness) {
	vec3    toEye   = normalize(-position); // Eye-space position, just need to normalize.
	Surface surface = Surface(normal, toEye, roughness);

	// Clear the light intensity accumulators
	Ambient  = vec3(0.0);
	Diffuse  = vec3(0.0);
	Specular = vec3(0.0);

	if (dot(vStaticLightDirOut, vStaticLightDirOut) > 0.0) {
		getContribution(surface, Light(vStaticLightDirOut.xyz, /*ambient =*/vec3(0.0), /*color =*/vColorOut.rgb, NO_ATTENUATION));
	} else {
		Ambient = vColorOut.rgb;
	}

	for (int light = 0; light < numLights; light++) {
		getContribution(surface, Light(/*direction =*/lightPosition[light].xyz - position, lightAmbient[light].rgb, lightDiffuse[light].rgb, lightQuadraticAtten[light]));
	}
}

//=============================================================================
//
// buildTSB()
//
// Construct our tangent-space basis matrix from the input data
//
//=============================================================================
mat3 buildTSB() {
    vec3 vNormal  = normalize(vNormalOut);
    // Gram-Schmidt orthogonalization.
    vec3 vTangent = normalize(vTangentOut - vNormal * dot(vTangentOut, vNormal));
    
    return mat3(vTangent, cross(vNormal, vTangent) * fTextureHandedness, vNormal);
}

//=============================================================================
//
// hableFilm()
//
// John Hable/'Uncharted 2' tone curve. Includes gamma 2.2 conversion,
// we probably need to come up with new coefficients if we ever move to native
// sRGB framebuffers so we don't double-correct output
//=============================================================================
vec3 hableFilm(vec3 x) {
    float a = 6.2;
    float b = 0.5;
    float c = 6.2;
    float d = 1.7;
    float e = 0.06;
    return x*(a*x+b)/(x*(c*x+d)+e);
}

//=============================================================================
//
// acesFilm()
//
// ACES standard photographic stock tone curve.
//=============================================================================
vec3 acesFilm(vec3 x) {
    float a = 2.51;
    float b = 0.03;
    float c = 2.43;
    float d = 0.59;
    float e = 0.14;
    return x*(a*x+b)/(x*(c*x+d)+e);
}

void main (void) {
	vec4  baseColor = (texture0Bound != 0) ? asLinear(texture2D(texUnit0, vTc.xy)) : vec4(1.0);
	vec4  specular  = asLinear(texture2D(texUnit2, vTc.xy));
	vec3  normal    = normalize(buildTSB() * (texture2D(texUnit1, vTc.xy).rgb * (255.0/127.0) - (128.0/127.0)));
	float opacity   = vColorOut.a * materialFrontDiffuse.a * baseColor.a;

	computeLighting(normal, vPosOut.xyz, asRoughness(specular.a));

	vec3 color = baseColor.rgb * (materialFrontEmissive.rgb + frontLightModelProductSceneColor.rgb + Ambient * materialFrontAmbient.rgb + Diffuse * materialFrontDiffuse.rgb) +
    			 specular.rgb * Specular;

	if (fogEnabled != 0) {
		color.rgb = mix(color.rgb, fogColor.rgb, saturate(fFogFragCoord));
	}

#if PSEUDO_HDR
	// Fake over-exposure for pseudo-HDR.
	gl_FragColor = vec4(hableFilm(color * fHdrExposure), opacity);
#else
	gl_FragColor = vec4(asGamma(color), opacity);
#endif
}