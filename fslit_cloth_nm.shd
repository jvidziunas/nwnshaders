/*

fslit_cloth_nm.shd

*/
#include "common"
#include "lit_common"
#include "noise"
#include "sdf"

#define MAX_LIGHTS 8
#define NO_ATTENUATION 0.0

uniform sampler2D texUnit0;	// base map
uniform sampler2D texUnit1;	// normal map
uniform sampler2D texUnit2;	// specular map

uniform         int   texture0Bound;

uniform lowp    vec4  fogColor;

uniform lowp	vec4  lightAmbient        [MAX_LIGHTS];
uniform lowp	vec4  lightDiffuse        [MAX_LIGHTS];
uniform mediump	vec4  lightPosition       [MAX_LIGHTS];
uniform mediump float lightQuadraticAtten [MAX_LIGHTS];
uniform         int   numLights;

uniform mediump	vec4  materialFrontAmbient;
uniform mediump	vec4  materialFrontDiffuse;
uniform mediump	vec4  materialFrontSpecular;
uniform mediump	vec4  materialFrontEmissive;
uniform lowp    vec4  frontLightModelProductSceneColor;

uniform         int   keyholeCanDissolve;
uniform         vec3  playerPosition;
uniform         vec3  cameraPosition;

varying         vec4  vPosOut;
varying highp   vec3  vWorldCoord;
varying         vec2  vTc;
varying         vec4  vColorOut;
varying         vec3  vStaticLightDirOut;
varying         vec3  vTangentOut;
varying         vec3  vNormalOut;
varying         float fTextureHandedness;
varying         float fFogFragCoord;

vec3 Ambient;
vec3 Diffuse;
vec3 Specular;

struct Surface {
	vec3  normal;
	vec3  toEye;
	float roughness;
};

struct Light {
	vec3  direction;
	vec3  ambient;
	vec3  color;
	float attenuationCoefficient;
};

void shadeSurface(in Surface surface, in Light light) {
	vec3  toLight     = normalize(light.direction);
	vec3  halfVector  = normalize(surface.toEye + toLight);
	float attenuation = 1.0 / (light.attenuationCoefficient * dot(light.direction, light.direction) + 1.0);
	float nDotH       = saturate(dot(surface.normal, halfVector));
	float nDotE       = dot(surface.normal, surface.toEye);
	float nDotL       = dot(surface.normal, toLight);

	float d    = getCharlieD(nDotH, surface.roughness);
	float g    = getAshikhminVisibility(nDotE, nDotL);
	vec3 color = attenuation * light.color;

	Ambient  += attenuation * light.ambient;
	Diffuse  += saturate(nDotL) * color;
	Specular += saturate(nDotL * 3.0) * (d * g) * color;
}

//=============================================================================
//
// computeLighting()
//
// Calculates lighting contribution for all lights.
//
//=============================================================================
void computeLighting(in vec3 normal, in vec3 position, in float roughness) {
	vec3    toEye   = normalize(-position); // Eye-space position, just need to normalize.
	Surface surface = Surface(normal, toEye, roughness);

	Ambient  = vec3(0.0);
	Diffuse  = vec3(0.0);
	Specular = vec3(0.0);

	if (anyNonzero(vStaticLightDirOut)) {
		shadeSurface(surface, Light(vStaticLightDirOut, /*ambient =*/vec3(0.0), /*color =*/vColorOut.rgb, NO_ATTENUATION));
	} else {
		Ambient = vColorOut.rgb;
	}

	for (int light = 0; light < numLights; light++) {
		shadeSurface(surface, Light(/*direction =*/lightPosition[light].xyz - position, lightAmbient[light].rgb, lightDiffuse[light].rgb, lightQuadraticAtten[light]));
	}
}

void main(void) {
	// Isolate this conditional in hopes that the driver will use dynamic branching.
    if (keyholeCanDissolve != 0) {
        if ((keyholeSdf(playerPosition, cameraPosition, vWorldCoord) + 0.2 * gradientNoise(vPosOut.xyz * 8.0)) < 0.0) {
            discard;
        }
    }

	vec4  baseColor = (texture0Bound != 0) ? asLinear(texture2D(texUnit0, vTc.xy)) : vec4(1.0);
	vec4  specular  = asLinear(texture2D(texUnit2, vTc.xy));
	vec3  normal    = normalize(buildTSB(vNormalOut, vTangentOut, fTextureHandedness) * unpackNormal(texture2D(texUnit1, vTc.xy).rgb));
	float opacity   = vColorOut.a * materialFrontDiffuse.a * baseColor.a;

	computeLighting(normal, vPosOut.xyz, asRoughness(specular.a));

	vec3 color = baseColor.rgb * (materialFrontEmissive.rgb + frontLightModelProductSceneColor.rgb + Ambient * materialFrontAmbient.rgb + Diffuse * materialFrontDiffuse.rgb) +
    			 specular.rgb  * Specular;

	// Gamma correct/tonemap before blending/raster out.
    gl_FragColor = tonemapOutput(mix(color, fogColor.rgb, saturate(fFogFragCoord)), opacity);
}