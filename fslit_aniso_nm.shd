/*

fslit_aniso_nm.shd

*/
#include "common"
#include "lit_common"
#include "sdf"

#define MAX_LIGHTS 8

uniform sampler2D texUnit0;	// base map
uniform sampler2D texUnit1;	// normal map
uniform sampler2D texUnit2;	// specular map
uniform sampler2D texUnit3; // multipurpose map

uniform         int  texture0Bound;

uniform lowp    vec4 fogColor;

uniform lowp	vec4  lightAmbient[MAX_LIGHTS];
uniform lowp	vec4  lightDiffuse[MAX_LIGHTS];
uniform mediump	vec4  lightPosition[MAX_LIGHTS];
uniform mediump float lightQuadraticAtten[MAX_LIGHTS];
uniform         int   numLights;

uniform mediump	vec4  materialFrontAmbient;
uniform mediump	vec4  materialFrontDiffuse;
uniform mediump	vec4  materialFrontEmissive;
uniform lowp    vec4  frontLightModelProductSceneColor;

uniform         int   keyholeCanDissolve;
uniform         vec3  playerPosition;
uniform         vec3  cameraPosition;

uniform         int   worldtimerTimeOfDay;

uniform         float anisotropy;

varying         vec4  vPosOut;
varying highp   vec3  vWorldCoord;
varying         vec2  vTc;
varying         vec4  vColorOut;
varying         vec3  vStaticLightDirOut;
varying         vec3  vNormalOut;
varying         vec3  vTangentOut;
varying lowp    float fTextureHandedness;
varying         float fFogFragCoord;

vec3 Ambient;
vec3 Diffuse;
vec3 Specular;

struct Surface {
	vec3  tangent;
	vec3  bitangent;
	vec3  normal;
	vec3  toEye;
	float alphaT;
	float alphaB;
	float smithE;
};

struct Light {
	vec3  direction;
	vec3  ambient;
	vec3  color;
	float attenuationCoefficient;
};

void shadeSurface(in Surface surface, in Light light) {
	vec3  toLight     = normalize(light.direction);
	vec3  halfVector  = normalize(surface.toEye + toLight);
	float attenuation = 1.0 / (light.attenuationCoefficient * dot(light.direction, light.direction) + 1.0);
	float tDotH       = dot(surface.tangent, halfVector);
	float bDotH       = dot(surface.bitangent, halfVector);
	float nDotH       = saturate(dot(surface.normal, halfVector));
	float tDotL       = dot(surface.tangent, toLight);
	float bDotL       = dot(surface.bitangent, toLight);
	float nDotL       = dot(surface.normal, toLight);

	float f    = fresnelReflectance(nDotH);
	float d    = getGgx(tDotH, bDotH, nDotH, surface.alphaT, surface.alphaB);
	float g    = getSmithVisibility_fast(surface.smithE, dot(surface.normal, surface.toEye), tDotL, bDotL, nDotL, surface.alphaT, surface.alphaB);
	vec3 color = attenuation * light.color;

	Ambient  += attenuation * light.ambient;
	Diffuse  += saturate(nDotL) * color;
	Specular += saturate(nDotL * 3.0) * (f * d * g) * color;
}

//=============================================================================
//
// computeLighting()
//
// Calculates lighting contribution for all lights.
//
//=============================================================================
void computeLighting(in vec3 tangent, in vec3 bitangent, in vec3 normal, in vec3 position, in float roughnessT, in float roughnessB) {
	vec3    toEye   = normalize(-position); // Eye-space position, just need to normalize.
	float   alphaT  = roughnessT * roughnessT;
	float   alphaB  = roughnessB * roughnessB;
	float   smithE  = getSmithTerm(dot(tangent, toEye), dot(bitangent, toEye), dot(normal, toEye), alphaT, alphaB);
	Surface surface = Surface(tangent, bitangent, normal, toEye, alphaT, alphaB, smithE);

	// Clear the light intensity accumulators
	Ambient  = vec3(0.0);
	Diffuse  = vec3(0.0);
	Specular = vec3(0.0);

	if (anyNonzero(vStaticLightDirOut)) {
		shadeSurface(surface, Light(vStaticLightDirOut, /*ambient =*/vec3(0.0), /*color =*/vColorOut.rgb, NO_ATTENUATION));
	} else {
		Ambient = vColorOut.rgb;
	}

	for (int light = 0; light < numLights; light++) {
		shadeSurface(surface, Light(/*direction =*/lightPosition[light].xyz - position, lightAmbient[light].rgb, lightDiffuse[light].rgb, lightQuadraticAtten[light]));
	}
}

void main(void) {
	vec4  baseColor = (texture0Bound != 0) ? asLinear(texture2D(texUnit0, vTc.xy)) : vec4(1.0);
	mat3  mTSB      = buildTSB(vNormalOut, vTangentOut, fTextureHandedness);
	vec3  normal    = normalize(mTSB * unpackNormal(texture2D(texUnit1, vTc.xy).rgb));
	vec4  specular  = asLinear(texture2D(texUnit2, vTc.xy));
	float opacity   = vColorOut.a * materialFrontDiffuse.a * texture2D(texUnit3, vTc.xy).r;

	/* HACK: We're converting to a Blinn-Phong exponent and then immediately transitioning to a Beckmann roughness value.
	 * It makes much more sense to move directly from the alpha value directly to roughness coefficient, possibly
	 * using a polynomial/rational function approximation for this conversion. */
	float roughness = asRoughness(mix(fShininessMin, fShininessMax, specular.a));
	computeLighting(/*tangent =*/mTSB[0], /*bitangent =*/mTSB[1], normal, vPosOut.xyz, roughness * (1.0 + anisotropy), roughness * (1.0 - anisotropy));

	vec3 color = baseColor.rgb * (materialFrontEmissive.rgb + frontLightModelProductSceneColor.rgb + Ambient * materialFrontAmbient.rgb + Diffuse * materialFrontDiffuse.rgb) +
    			 specular.rgb * Specular;

    if (keyholeCanDissolve != 0) { // Isolate this conditional in hopes that the driver will use dynamic branching.
        float keyholeDistance = keyholeSdf(playerPosition, cameraPosition, vWorldCoord) + KEYHOLE_BREAKUP_SCALE * simplexNoise(vWorldCoord.xy * KEYHOLE_NOISE_SCALE);
        clip(keyholeDistance);
        color *= smoothstep(0.0, KEYHOLE_DARK_BAND_SIZE, keyholeDistance); // Darken edges. Clipped areas will end up black, but we can't see them anyway.
    }

    gl_FragColor = tonemapOutput(mix(color, fogColor.rgb, saturate(fFogFragCoord)), opacity); // Gamma correct/tonemap before blending/raster out.
}